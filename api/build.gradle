buildscript {
    ext {
        junitVersion = '5.3.2'
        springFoxVersion = '2.9.2'
        hikariCPVersion = '3.3.1'
        flywayVersion = '5.2.4'
        postgresJdbcVersion = '42.2.5'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        // to fix an issue exposed by spotbugs 2.0.0: https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
        classpath "com.google.guava:guava:27.0.1-jre"
    }
}

plugins {
    id "org.springframework.boot" version "2.2.0.RELEASE"
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id "org.owasp.dependencycheck" version "4.0.0.1"
    id "com.github.spotbugs" version "2.0.0"
    id 'pmd'
    id 'checkstyle'
}
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
    }
}

lombok {
    version = '1.18.4'
    sha256 = ""
}

group = 'uk.gov.dhsc.htbhf'
sourceCompatibility = 1.11

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/departmentofhealth-htbhf/maven"
    }
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.springframework.security:spring-security-core:5.1.5.RELEASE"
    implementation "com.zaxxer:HikariCP:${hikariCPVersion}"
    implementation "org.postgresql:postgresql:${postgresJdbcVersion}"
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "commons-codec:commons-codec:1.12"
    implementation "commons-io:commons-io:2.5"
    implementation "io.springfox:springfox-swagger2:${springFoxVersion}"
    implementation "io.springfox:springfox-bean-validators:${springFoxVersion}"
    // Need to upgrade the version of Guava to address Snyk vulnerability found in SpringFox.
    // https://app.snyk.io/vuln/SNYK-JAVA-COMGOOGLEGUAVA-32236
    implementation "com.google.guava:guava:27.0.1-jre"
    implementation "com.google.code.findbugs:annotations:3.0.1"
    implementation "uk.gov.dhsc.htbhf:htbhf-common-rest:latest.release"
    implementation "uk.gov.dhsc.htbhf:htbhf-common-logging:latest.release"
    implementation "net.javacrumbs.shedlock:shedlock-spring:2.4.0"
    implementation "net.javacrumbs.shedlock:shedlock-provider-jdbc-template:2.4.0"
    implementation "com.vladmihalcea:hibernate-types-52:2.4.3"
    implementation "org.javers:javers-spring-boot-starter-sql:5.7.7:"
    implementation "org.apache.commons:commons-lang3:3.9"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "io.micrometer:micrometer-registry-prometheus:1.1.4"
    implementation 'uk.gov.service.notify:notifications-java-client:3.14.2-RELEASE'
    implementation "uk.gov.dhsc.htbhf:htbhf-common-database:latest.release"
    implementation "uk.gov.dhsc.htbhf:htbhf-common-dwp-api:latest.release"
    implementation project(':htbhf-claimant-service-db')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mockito:mockito-junit-jupiter:2.23.4"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "io.zonky.test:embedded-database-spring-test:1.4.1"
    testImplementation "org.testcontainers:postgresql:1.10.6"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner:2.1.1.RELEASE"
    testImplementation "uk.gov.dhsc.htbhf:htbhf-common-test:latest.release"
    testImplementation "uk.gov.dhsc.htbhf:htbhf-common-dwp-api-test:latest.release"

    // When editing the api documentation, this is useful to be able to view the swagger-ui on http://localhost:8080/swagger-ui.html
    // implementation "io.springfox:springfox-swagger-ui:${springFoxVersion}"

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.9.0'
}


test {
    useJUnitPlatform()
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    toolVersion "8.15"
}

pmdMain {
    ignoreFailures = false
    ruleSetFiles = files("../config/pmd/rulesets.xml")
    ruleSets = [] // Remove built-in rule sets
}
pmdTest {
    ignoreFailures = false
    ruleSetFiles = files("../config/pmd/test-rulesets.xml")
    ruleSets = [] // Remove built-in rule sets
}
spotbugsTest {
    excludeFilter = file("../config/spotbugs/test-exclude.xml")
}

task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    classpath += sourceSets.main.runtimeClasspath
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = true
}

bootRun {
    systemProperties System.properties
    args = ["--spring.profiles.active=local-development"]
}
