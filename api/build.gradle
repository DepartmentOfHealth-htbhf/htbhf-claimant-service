buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
        spockVersion = '1.2-groovy-2.4'
        junitVersion = '5.3.2'
        springFoxVersion = '2.9.2'
        jacksonVersion = '2.9.8'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id "org.owasp.dependencycheck" version "4.0.0.1"
    id "com.github.spotbugs" version "1.6.5"
    id 'pmd'
    id 'checkstyle'
}
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

lombok {
    version = '1.18.4'
    sha256 = ""
}

group = 'uk.gov.dhsc.htbhf'
sourceCompatibility = 1.11

repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // spring-boot-start-web includes insecure version of databind. Upgrading to fixed version until this is fixed in spring-boot-starter-web.
    // https://app.snyk.io/org/departmentofhealth-htbhf/project/c7503341-dc5a-4940-8620-9720ffc8ae26?utm_campaign=weekly_report&utm_source=Project
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.zaxxer:HikariCP'
    implementation 'org.postgresql:postgresql'
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "commons-io:commons-io:2.5"
    implementation "io.springfox:springfox-swagger2:${springFoxVersion}"
    implementation "io.springfox:springfox-bean-validators:${springFoxVersion}"
    // Need to upgrade the version of Guava to address Snyk vulnerability found in SpringFox.
    // https://app.snyk.io/vuln/SNYK-JAVA-COMGOOGLEGUAVA-32236
    implementation "com.google.guava:guava:27.0.1-jre"
    implementation "com.google.code.findbugs:annotations:3.0.1"
    implementation project(':htbhf-claimant-service-db')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:2.23.4"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "com.h2database:h2:1.4.197"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"


    // When editing the api documentation, this is useful to be able to view the swagger-ui on http://localhost:8080/swagger-ui.html
    // implementation "io.springfox:springfox-swagger-ui:${springFoxVersion}"
}


test {
    useJUnitPlatform()
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    toolVersion "8.15"
}

pmdMain {
    ignoreFailures = false
    ruleSetFiles = files("../config/pmd/rulesets.xml")
    ruleSets = [] // Remove built-in rule sets
}
pmdTest {
    ignoreFailures = false
    ruleSetFiles = files("../config/pmd/test-rulesets.xml")
    ruleSets = [] // Remove built-in rule sets
}
spotbugsTest {
    excludeFilter = file("../config/spotbugs/test-exclude.xml")
}

task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    classpath += sourceSets.main.runtimeClasspath
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = true
}

