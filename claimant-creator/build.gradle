buildscript {
    ext {
        hikariCPVersion = '3.3.1'
        postgresJdbcVersion = '42.2.5'
        jacksonDatabindVersion = '2.9.9.1'
        jacksonDataformatVersion = '2.9.9'
        jacksonDatatypeVersion = '2.9.9'
        springBootVersion = '2.1.8.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        // to fix an issue exposed by spotbugs 2.0.0: https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
        classpath "com.google.guava:guava:27.0.1-jre"
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
}
apply plugin: 'java'

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
    }
}

lombok {
    version = '1.18.4'
    sha256 = ""
}

group = 'uk.gov.dhsc.htbhf'
sourceCompatibility = 1.11

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/departmentofhealth-htbhf/maven"
    }
}


dependencies {
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonDataformatVersion}"
    implementation "com.zaxxer:HikariCP:${hikariCPVersion}"
    implementation "org.postgresql:postgresql:${postgresJdbcVersion}"
    implementation "org.apache.commons:commons-lang3:3.9"
    // Need to upgrade the version of Guava to address Snyk vulnerability found in SpringFox.
    // https://app.snyk.io/vuln/SNYK-JAVA-COMGOOGLEGUAVA-32236
    implementation "com.vladmihalcea:hibernate-types-52:2.4.3"
}

task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    classpath += sourceSets.main.runtimeClasspath
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = true
}

